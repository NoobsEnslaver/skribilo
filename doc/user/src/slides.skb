(use-modules (skribilo package slide))

(document :title (color :fg "red" (sf "Skribilo Slides"))
   :author (author :name (it [Bob Smith])
	      :affiliation [The Organization]
	      :address (ref :url "http://example.org/"))

   (slide :title "Table of Contents" :number #f

      ;; "Manually" produce a list of the slides.  This works by traversing
      ;; the whole document tree, looking for `slide' nodes.
      (p
       (resolve (lambda (n e env)
                  (let ((slides (search-down (lambda (n)
                                               (is-markup? n 'slide))
                                             (ast-document n))))
                    (itemize
                     (map (lambda (n)
                            (item (ref :handle (handle n)
                                       :text (markup-option n :text))))
                          slides)))))))

   (slide :title "Introduction" :vspace 0.3

      (p [This is a simple slide, not grouped in any topic.]))

   (slide-topic :title "Interactive Features" :outline? #t

     (slide :title "X11 Clients" :toc #t :vspace 0.3

	(itemize
	   (item "xlock")
	   (item "xeyes")
	   (item "xterm")))

     (slide :title "Xclock" :toc #t :vspace 0.3

	(center (sf (underline "The Unix xclock client")))
	(slide-vspace 0.3)

	(slide-pause)
	(slide-embed :command "xlock"
		     :alt (frame "Can't run embedded application")))))
