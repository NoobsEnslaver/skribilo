;;; slide.skb  --  Slides
;;; -*- coding: iso-8859-1 -*-
;;;
;;; Copyright 2006, 2008, 2009  Ludovic Courtès <ludo@gnu.org>
;;; Copyright 2004  Manuel Serrano
;;;
;;;
;;; This file is part of Skribilo.
;;;
;;; Skribilo is free software: you can redistribute it and/or modify
;;; it under the terms of the GNU General Public License as published by
;;; the Free Software Foundation, either version 3 of the License, or
;;; (at your option) any later version.
;;;
;;; Skribilo is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with Skribilo.  If not, see <http://www.gnu.org/licenses/>.


;*---------------------------------------------------------------------*/
;*    dummy-slide-set-output ...                                       */
;*---------------------------------------------------------------------*/
(define dummy-slide-set-output
   (lambda (n e)
      (let* ((a (markup-option n :author))
	     (t (markup-option n :title))
	     (b (markup-body n))
	     (ta (table (tr (map (lambda (n)
				    (td :valign 'top :align 'center n))
	                         (if (list? a) a (list a)))))))
	 (evaluate-document (center (bold t)) e)
	 (evaluate-document (center ta) e)
	 (output b e))))

(define dummy-slide-output
   (lambda (n e)
      (let* ((t (markup-option n :title))
	     (b (markup-body n)))
	 (evaluate-document (bold t) e)
	 (output b e))))

(define dummy-slide-vspace-output
   (lambda (n e)
     (evaluate-document (linebreak) e)
     (evaluate-document (center (tt "[vspace]")) e)
     (evaluate-document (linebreak) e)))

(define dummy-slide-embed-output
   (lambda (n e)
     (evaluate-document (linebreak) e)
     (evaluate-document (tt (or (markup-option n :alt)
			  (markup-option n :command)))
		  	e)))

;*---------------------------------------------------------------------*/
;*    Computer programs                                                */
;*---------------------------------------------------------------------*/
(chapter :title "Slide Package"
         :ident "slides"

   (p [This chapter describes the facilities Skribilo offers authoring
slides.  As usual, in order to access the functionalities described in
this chapter, the ,(code [(use-modules (skribilo package slide))])
expression must be introduced at the beginning of the document.])

   ;; FIXME: The following paragraph describes inaccessible features.
   #;(p [The Slide package supports two ,(markup-ref "skribe-load") user
options: ,(param :advi) and ,(param :prosper).  The first one tells
Skribe to generate slides for the Advi presenter. The second one tells
Skribe to generate slides for the LaTeX prosper package.])

;*---------------------------------------------------------------------*/
;*    slide ... @label slide@                                          */
;*---------------------------------------------------------------------*/
(section :title "Slides and Slide Topics"
   
   (p [A ,(code "slide") function call creates a slide.])
   
   (doc-markup 'slide
	       `((:title ,[The title of the slide.])
		 (:number ,[The number of the slide (a boolean or an integer).])
		 (:toc ,[This argument controls if the slide must be
referenced in the ,(ref :mark "toc" :text "table of contents") and the
outline slide that introduces a ,(markup-ref "slide-topic") (see
below).])
		 (:vspace ,[The boolean ,(code "#f") or an integer representing
the vertical space size between the title and the body of the slide.])
		 (:vfill ,[A boolean that is used to control whether a LaTeX
,(code "\\vfill") is issued at the end of the slide.])
		 (:transition ,[The boolean ,(code "#f") or a symbol in the 
list ,(code "(split blinds box wipe dissolve glitter)").])
		 (:bg ,[The background color of the slide.])
		 (:image ,[The background image of the slide.]))
	       :source "skribilo/package/slide.scm")
   
    (p [Optionally, one may group slides into ,(emph [topics]) and
,(emph [subtopics]).  Topics and subtopics are just logical grouping
under a given title that are meant to provide structure to a set of
slides.  With their ,(param :outline?) option, these two markups can be
used to automatically produce an outline at the beginning of each new
(sub)topic, which reminds the audience of the current position within
the talk.])
   
   (doc-markup 'slide-topic
	       `((:title ,[The title of a topic.])
	         (:outline? ,[A boolean (i.e., ,(code [#t]) or ,(code
[#f])) telling whether an outline should be produced at the beginning of
this topic.  The outline will typically list the titles of the different
topics, as well as the titles of the slides under the current topic.
Note that ,(markup-ref "slide")s whose ,(param :toc) option is ,(code
[#f]) will not be shown in the outline.])
	         (:unfold? ,[If ,(code [#t]), then the outline will also
show the contents of the current topic.])
		 (:toc ,[This argument controls if the slide must be
referenced in the ,(ref :mark "toc" :text "table of contents") and the
outline slide that introduces a ,(markup-ref "slide-topic") (see
below).]))
	       :others '(slide-subtopic)
	       :source "skribilo/package/slide.scm")

   (p [This package understands the following additional customs (see
Section ,(ref :subsection "Engine Customs" :page #t)):

,(description
    (item :key (tt [slide-outline-title])
       [The title of outline slides.  By default, no title is
produced.])
    (item :key (tt [slide-outline-active-color])
       [The color in which the current slide topic is displayed in
outline slides.])
    (item :key (tt [slide-outline-inactive-color])
       [The color in which inactive slide topics are displayed in
outline slides.]))]))

;*---------------------------------------------------------------------*/
;*    slide-pause                                                      */
;*---------------------------------------------------------------------*/
(section :title "Pause"
   
   (p [A ,(code "slide-pause") function call introduces a pause in the slide
projection.  This may not be supported by all engines.])
   
   (doc-markup 'slide-pause
	       '()
	       :common-args '()
	       :source "skribilo/package/slide.scm"))

;*---------------------------------------------------------------------*/
;*    slide-vspace ...                                                 */
;*---------------------------------------------------------------------*/
(section :title "Slide Vertical Space"
   
   (p [The ,(code "slide-vspace") introduces a vertical space in the slide.])
   
   (doc-markup 'slide-vspace
	       '((:unit [The unit of the space.])
		 (#!rest val [The size of the vertical space.]))
	       :common-args '()
	       :source "skribilo/package/slide.scm"))

;*---------------------------------------------------------------------*/
;*    slide-embed ...                                                  */
;*---------------------------------------------------------------------*/
(section :title "Slide Embed Applications"
   
   (p [Embed an application inside a slide.])
   
   (doc-markup 'slide-embed
	       `((:command [The binary file for running the embedded 
application.])
	         (:arguments [Additional arguments to be passed to the
application (a list of strings).])
		 (:geometry-opt [The name of the geometry option to be sent
to the embedded application.])
		 (:geometry [The geometry value to be sent.])
		 (:rgeometry [A relative geometry to be sent.])
		 (:transient-opt [The name of the transient option to be sent
to the embedded application.])
		 (:transient [The transient value to be sent.])
		 (:alt [An alternative Skribilo expression to be used if the
output format does not support embedded application.]))
	       :common-args '()
	       :source "skribilo/package/slide.scm"))

;*---------------------------------------------------------------------*/
;*    Example                                                          */
;*---------------------------------------------------------------------*/
(section :title "Example"
         :info-node "Slide Example"

   (p [Here is a complete example of Skribilo slides:])
   
   (if (and (engine-format? "html") 
	    (not (equal? (engine-custom (find-engine 'html) 'html-variant)
	         "html4")))
       ;; Show the example and its result
       (example-produce
         (example :legend "Example of Skribilo slides"
		  (prgm :file "src/slides.skb"))
	 (disp
	  (processor :combinator
		     (lambda (e1 e2)
		       (let ((e (copy-engine 'document-engine e2)))
			 (markup-writer 'document e
					:options '(:title :author :ending)
					:action dummy-slide-set-output)
			 (markup-writer 'slide e
					:options '(:title :ident
						   :number :toc :vspace)
					:action dummy-slide-output)
			 (markup-writer 'slide-vspace
					:action dummy-slide-vspace-output)
			 (markup-writer 'slide-embed
			                :options '(:command :arguments :alt)
					:action dummy-slide-embed-output)
			 e))
		     (include "src/slides.skb"))))
       ;; Show only the example (i.e. don't produce a document in a document)
       (example :legend "Example of Skribilo slides"
	       (prgm :file "src/slides.skb")))))

;;; Local Variables:
;;; ispell-local-dictionary: "american"
;;; End:
